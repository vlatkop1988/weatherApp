plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
}
ext {
    BASE_URL = "BASE_URL"
    developBaseUrl = "\"https://api.openweathermap.org/data/2.5/\""
    prodBaseUrl = "\"https://api.openweathermap.org/data/2.5/\""
}
android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "com.vlatko.weatherapp"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "WeatherApp-${defaultConfig.versionName}-(${defaultConfig.versionCode})-${variant.flavorName}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    ext {
        environment = "environment"
    }

    flavorDimensions environment
    productFlavors {
        develop {
            dimension environment
            applicationIdSuffix ".develop.test"
            buildConfigField "String", 'BASE_URL', developBaseUrl
        }
        production {
            dimension environment
            buildConfigField "String", 'BASE_URL', prodBaseUrl
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name

        def productionCriteria = (names.contains("production") && variant.buildType.name == "debug")
        def developmentCriteria = (names.contains("develop") && variant.buildType.name == "release")
        if (productionCriteria || developmentCriteria) {
            setIgnore(true)
        }
    }
}



dependencies {
    implementation project(path: ':data_local_storage')
    implementation project(path: ':presentation')
    implementation project(path: ':domain')
    implementation project(path: ':data_network')
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
apply from: '../dep_common.gradle'
apply from: '../dep_rx_java.gradle'